# CMakeFiles.txt for the minc-man-pages
#
# Vladimir S. FONOV - vladimir.fonov@gmail.com

SET(MANPAGES_PACKAGE_VERSION_MAJOR 0)
SET(MANPAGES_PACKAGE_VERSION_MINOR 0)
SET(MANPAGES_PACKAGE_VERSION_PATCH 99)


IF(NOT MINC_TOOLKIT_BUILD)
cmake_minimum_required(VERSION 2.8)
# Packaging defines
SET(CPACK_GENERATOR TGZ)
SET(CPACK_PACKAGE_VERSION_MAJOR ${MANPAGES_PACKAGE_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${MANPAGES_PACKAGE_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${MANPAGES_PACKAGE_VERSION_PATCH})


PROJECT(minc-man-pages LANGUAGES NONE)

ENDIF(NOT MINC_TOOLKIT_BUILD)

SET(PACKAGE "minc-man-pages")
SET(PACKAGE_BUGREPORT "vladimir.fonov@gmail.com")

SET(PACKAGE_NAME "minc-man-pages")
SET(PACKAGE_VERSION "${MANPAGES_PACKAGE_VERSION_MAJOR}.${MANPAGES_PACKAGE_VERSION_MINOR}.${MANPAGES_PACKAGE_VERSION_PATCH}")
SET(PACKAGE_STRING "${PACKAGE_NAME} ${PACKAGE_VERSION}")


SET(CPACK_PACKAGE_FILE_NAME "minc-man-pages-${PACKAGE_VERSION}")
SET(CMAKE_INSTALL_PREFIX ${CMAKE_CURRENT_BINARY_DIR} CACHE PATH "Install path prefix, prepended onto install directories.")
SET(CPACK_SET_DESTDIR    ON)
SET(CPACK_PACKAGING_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})



file(GLOB MAN_FILES _man/*.md)
SET(SECT 1)
file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/man/man${SECT})
SET(OUTPUT_MAN_FILES )
foreach(i IN LISTS MAN_FILES)
  
  get_filename_component(n ${i} NAME_WE)

  add_custom_command(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/man/man${SECT}/${n}.${SECT} 
    COMMAND pandoc -S -s -f markdown -t man ${i} -o ${CMAKE_CURRENT_BINARY_DIR}/man/man${SECT}/${n}.${SECT}
    DEPENDS ${i} 
  )

  add_custom_target(${n} ALL DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/man/man${SECT}/${n}.${SECT} )

  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/man/man${SECT}/${n}.${SECT} DESTINATION  ${CMAKE_INSTALL_PREFIX}/man/man${SECT}/)
  LIST(APPEND OUTPUT_MAN_FILES man/man${SECT}/${n}.${SECT})
endforeach()

add_custom_command(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE}-${PACKAGE_VERSION}.tar.gz
  COMMAND cmake -E tar czf ${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE}-${PACKAGE_VERSION}.tar.gz ${OUTPUT_MAN_FILES} 
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS ${OUTPUT_MAN_FILES} 
)

add_custom_target(OUTPUT_MAN_FILES ALL DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE}-${PACKAGE_VERSION}.tar.gz )
  
IF(NOT MINC_TOOLKIT_BUILD)
INCLUDE(CPack)
ENDIF(NOT MINC_TOOLKIT_BUILD)
